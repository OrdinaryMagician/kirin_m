// armor for Kirin mod

Class KirinInternalArmor : SWWMArmor
{
	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
		Inventory.InterHubAmount 1;
		SWWMArmor.ArmorPriority 1;
		+INVENTORY.UNCLEARABLE;
		+SWWMARMOR.NOHITFLASH;
		+SWWMARMOR.NOHITSOUND;
		+SWWMARMOR.NODRAIN;
	}

	override bool HandlePickup( Inventory item )
	{
		// disallow vanilla armors
		if ( (item is 'BasicArmor') || (item is 'BasicArmorBonus') || (item is 'BasicArmorPickup') || (item is 'HexenArmor') )
		{
			item.bPickupGood = true;	// but act as if we picked them up
			return true;
		}
		return false;
	}

	override int HandleDamage( int damage, Name damageType, int flags )
	{
		double dr = 0.;
		int dt = 0;
		switch ( damageType )
		{
		case 'Fire':
		case 'Lava':
		case 'Magma':
		case 'Heat':
		case 'Thermal':
			dr = .6;
			dt = 4;
			break;
		case 'Ice':
		case 'Cold':
			dr = .4;
			dt = 3;
			break;
		case 'Poison':
		case 'PoisonCloud':
			dr = .5;
			dt = 4;
			break;
		case 'Toxic':
		case 'Toxin':
			dr = .4;
			dt = 3;
			break;
		case 'Slime':
		case 'Acid':
			dr = .3;
			dt = 2;
			break;
		case 'Lightning':
		case 'Electricity':
		case 'Electric':
		case 'Plasma':
		case 'Chaos':
			dr = .5;
			dt = 3;
			break;
		case 'Bullet':
		case 'Hitscan':
		case 'Shot':
			dr = .5;
			dt = 2;
			break;
		case 'Melee':
			dr = .6;
			dt = 3;
			break;
		default:
			dr = .4;
			dt = 3;
			break;
		}
		// 25% general splash reduction
		if ( flags&DMG_EXPLOSION )
			dr = (.5+dr)/2;
		// damage resistance wavers with blood thinning
		if ( Owner.SpawnHealth() < Owner.default.Health )
			dr *= Owner.SpawnHealth()/double(Owner.default.Health);
		int newdamage = int(ceil(max(damage-dt,0)*(1.-dr)));
		return damage-newdamage;
	}
}
